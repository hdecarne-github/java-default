// Plugins
plugins {
	id "java-library"
	id "jacoco"
	id "maven-publish"
	id "eclipse"
	id "de.carne.java-tools" version "0.9.0"
	id "org.sonarqube" version "3.0"
	id "com.jfrog.artifactory" version "4.17.2" 
	id "com.jfrog.bintray" version "1.8.5"
}

// Build helpers
def snapshotBuild = project.version.endsWith("-SNAPSHOT")

def resolveProperty(propertyName, envName, defValue = "<undefined>") {
	return ((project.findProperty(propertyName) ?: System.getenv(envName)) ?: defValue);
}

// Project
defaultTasks "classes", "testClasses"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
	// Caused by org.eclipse.jdt.annotation
	options.compilerArgs += ["-Xlint:-requires-transitive-automatic"]
}

plugins.withType(JavaPlugin).configureEach {
	java {
		modularity.inferModulePath = true
		registerFeature('log4j') {
			usingSourceSet(sourceSets.main)
		}	
		registerFeature('slf4j') {
			usingSourceSet(sourceSets.main)
		}	
	}
}

repositories {
	if(snapshotBuild) {
		mavenLocal()
		maven { url "https://oss.jfrog.org/libs-snapshot/" }
	}
	jcenter()
	maven { url "https://oss.jfrog.org/libs-release/" }
}

dependencies {
	api(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: project.annotationVersion)
	log4jImplementation(group: "org.apache.logging.log4j", name: "log4j-api", version: project.log4jVersion)
	slf4jImplementation(group: "org.slf4j", name: "slf4j-api", version: project.slf4jVersion)
	testImplementation(group: "de.carne.common", name: "java-test", version: project.javaTestVersion)
	testImplementation(group: "org.apache.logging.log4j", name: "log4j-core", version: project.log4jVersion)
	testImplementation(group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: project.log4jVersion)
}

jar {
	manifest {
		attributes(
			'Created-By': "Gradle ${gradle.gradleVersion} (${System.properties['os.name']}:${System.properties['os.arch']}:${System.properties['os.version']})",
			'Build-Jdk' : System.properties['java.version'],
			'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd HH:mm:ssZ").format(new Date()),
			'X-Compile-Target-JDK': targetCompatibility,
			'X-Compile-Source-JDK': sourceCompatibility,
			'X-Module-Id': project.name,
			'X-Module-Name': project.name,
			'X-Module-Version': project.version,
			'X-Module-DocURL': projectUrl,
			'X-Module-License': projectLicenseUrl
		)
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "started", "failed", "passed", "skipped"
		exceptionFormat "full"
	}
	// Needed because Application class is statefull
	forkEvery = 1
}

jacoco {
	toolVersion = project.jacocoVersion
}

jacocoTestReport {
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/reports/jacoco/coverage.xml")
		html.enabled false
		csv.enabled false
	}
}
test.finalizedBy jacocoTestReport

sonarqube {
	properties {
		property 'sonar.coverage.jacoco.xmlReportPaths', jacocoTestReport.reports.xml.destination
	}
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
	classifier "sources"
}

task javadocJar(type: Jar) {
	from javadoc
	classifier = "javadoc"
}

publishing {
	publications {
		Bintray(MavenPublication) {
			from components.java
			suppressPomMetadataWarningsFor("log4jApiElements")
			suppressPomMetadataWarningsFor("log4jRuntimeElements")
			suppressPomMetadataWarningsFor("slf4jApiElements")
			suppressPomMetadataWarningsFor("slf4jRuntimeElements")
			artifact sourceJar
			artifact javadocJar
			pom {
				name = project.name
				description = projectDescription
				url = projectUrl
				licenses {
					license {
						name = projectLicense
						url = projectLicenseUrl
						distribution = "repo"
					}
				}
				developers {
					developer {
						id = project.resolveProperty("developerId", "DEVELOPER_ID")
						name = project.resolveProperty("developerName", "DEVELOPER_NAME")
						email = project.resolveProperty("developerEmail", "DEVELOPER_EMAIL")
					}
				}
				scm {
					url = projectScmUrl
				}
			}
		}
	}
}

artifactory {
	contextUrl = "https://oss.jfrog.org/artifactory"
	publish {
		repository {
			repoKey = (snapshotBuild ? "oss-snapshot-local" : "oss-release-local")
			username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
			password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
			maven = true
		}
		defaults {
			publications("Bintray")
		}
	}
	resolve {
		repoKey = (snapshotBuild ? "libs-snapshot" : "libs-release")
		username = project.resolveProperty("artifactory_user", "ARTIFACTORY_USER")
		password = project.resolveProperty("artifactory_password", "ARTIFACTORY_PASSWORD")
		maven = true
	}
	clientConfig.setIncludeEnvVars(false)
}

bintray {
	user = project.resolveProperty("bintrayUser", "BINTRAY_USER")
	key = project.resolveProperty("bintrayKey", "BINTRAY_KEY")
	publications = [ "Bintray" ]
	dryRun = (project.findProperty("bintray.dryRun") ?: false);
	publish = (project.findProperty("bintray.publish") ?: !snapshotBuild);
	override = (project.findProperty("bintray.override") ?: false);
	pkg {
		repo = "maven"
		name = project.name
		licenses = [ projectLicenseId ]
		vcsUrl = projectScmUrl
		version {
			gpg {
				sign = true
				passphrase = project.resolveProperty("bintrayGpgPassphrase", "BINTRAY_GPG_PASSPHRASE")
			}
			mavenCentralSync {
				user = project.resolveProperty("ossrhUsername", "OSSRH_USERNAME")
				password = project.resolveProperty("ossrhPassword", "OSSRH_PASSWORD")
			}
		}
	}
}

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
		file {
			whenMerged {
				def eeaPath = project.findProperty("eclipse.eeaPath");
				 
				entries.forEach {
					if(it.path.startsWith("org.eclipse.jdt.launching.JRE_CONTAINER")) {
						if(eeaPath != null) {
							it.entryAttributes.put("annotationpath", eeaPath)
						}
					} else if(it.path.contains("/caches/modules-2/")) {
						if(eeaPath != null) {
							it.entryAttributes.put("annotationpath", eeaPath)
						}
					}
				}
			}
		}
	}
}
